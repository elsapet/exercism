defmodule ProteinTranslation do
  @doc """
  Given an RNA string, return a list of proteins specified by codons, in order.
  """
  @spec of_rna(String.t()) :: {atom, list(String.t())}
  def of_rna(rna) do
    rna_to_protein(rna, [])
  end

  @doc """
  Given a codon, return the corresponding protein

  UGU -> Cysteine
  UGC -> Cysteine
  UUA -> Leucine
  UUG -> Leucine
  AUG -> Methionine
  UUU -> Phenylalanine
  UUC -> Phenylalanine
  UCU -> Serine
  UCC -> Serine
  UCA -> Serine
  UCG -> Serine
  UGG -> Tryptophan
  UAU -> Tyrosine
  UAC -> Tyrosine
  UAA -> STOP
  UAG -> STOP
  UGA -> STOP
  """

  @codon_to_protein %{
    "UGU" => "Cysteine",
    "UGC" => "Cysteine",
    "UUA" => "Leucine",
    "UUG" => "Leucine",
    "AUG" => "Methionine",
    "UUU" => "Phenylalanine",
    "UUC" => "Phenylalanine",
    "UCU" => "Serine",
    "UCC" => "Serine",
    "UCA" => "Serine",
    "UCG" => "Serine",
    "UGG" => "Tryptophan",
    "UAU" => "Tyrosine",
    "UAC" => "Tyrosine",
    "UAA" => "STOP",
    "UAG" => "STOP",
    "UGA" => "STOP"
  }

  @spec of_codon(String.t()) :: {atom, String.t()}
  def of_codon(codon) do
    case @codon_to_protein |> Map.has_key?(codon) do
      true -> {:ok, @codon_to_protein |> Map.get(codon)}
      _ -> {:error, "invalid codon"}
    end
  end

  defp rna_to_protein(<<codon::binary-size(3), remaining_rna::binary>>, acc) do
    case of_codon(codon) do
      {:error, _} ->
        {:error, "invalid RNA"}

      {:ok, "STOP"} ->
        {:ok, acc}

      {:ok, protein} ->
        rna_to_protein(remaining_rna, acc ++ [protein])
    end
  end

  defp rna_to_protein("", acc), do: {:ok, acc}
end
